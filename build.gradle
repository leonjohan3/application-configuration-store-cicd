plugins {
    id 'application'
    id "io.freefair.lombok" version "8.6"
    id 'jacoco'
    id "pmd"
    id 'checkstyle'
    id "com.github.spotbugs" version "6.0.15"
    id "org.sonarqube" version "5.0.0.4638"
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'software.amazon.awscdk:aws-cdk-lib:2.143.0'
    implementation 'software.constructs:constructs:10.3.0'
    implementation 'software.amazon.awssdk:appconfig:2.25.50'
    implementation 'commons-cli:commons-cli:1.8.0'
    implementation 'org.apache.commons:commons-lang3:3.14.0'

    testImplementation libs.junit.jupiter
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    // Define the main class for the application.
    mainClass = 'com.myorg.AppConfigStoreCdkApp'
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

pmd {
    consoleOutput = true
    toolVersion = "7.2.0"
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

checkstyle {
    toolVersion = "10.17.0"
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
    maxErrors = 1
    maxWarnings = 1
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}
